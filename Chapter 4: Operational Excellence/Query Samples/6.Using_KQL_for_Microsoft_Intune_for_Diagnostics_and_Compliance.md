## Using KQL for Microsoft Intune for Diagnostics and Compliance

#### KQL queries from the Using KQL for Microsoft Intune for Diagnostics and Compliance section of chapter 4 are found below

#### Use the copy option (to the right of each code box) to copy the query to paste into your own environment

#### Some queries use example data, or are based on hyopthetical scenarios, and you should switch in relevant data to your environment if required


##### Graphical Representation of Policy Changes by User
```KQL
IntuneAuditLogs
| project-rename User=Identity, Change=OperationName
| project TimeGenerated, Change, User
| summarize count() by User
| render columnchart
```

##### Finding Settings Changes in Policies
```KQL
IntuneAuditLogs
| where TimeGenerated >= ago(30d)
| where OperationName !contains "Assignment"
| parse Properties with * ',"TargetDisplayNames":["' Object '"],' *
| parse Properties with * '"TargetDisplayNames":["'IntuneProperty'"]' * ',"Targets":[{"ModifiedProperties":[{"' ModifiedProperties '],'*
| project TimeGenerated, Identity, Object, OperationName, ModifiedProperties
```

##### MSFT Employee Contribution-Mark Hopper
##### This query is helpful in analyzing audit activity specifically which objects (policies, apps, profiles, etc) have been deleted and by which identity within the last 7 days.
```KQL
IntuneAuditLogs
| where TimeGenerated > ago(7d)
| where ResultType == "Success"
| where OperationName has ("Delete")
| extend PropertiesJson = todynamic(Properties)
| extend ObjectNames = tostring(PropertiesJson["TargetDisplayNames"])  
| extend ObjectIds = tostring(PropertiesJson["TargetObjectIds"])
```

##### Hunting Specific Policy Group Assignment Changes
```KQL
IntuneAuditLogs
| where OperationName contains "Assignment"
| parse Properties with * '"TargetDisplayNames":["'IntuneProperty'"' * 'Target.GroupId","' GroupAssignmentChanges '(' *
| where IntuneProperty == "DJ-1"
| parse GroupAssignmentChanges with * 'New":"' Change
| project TimeGenerated, Identity, Policy=IntuneProperty, Operation=OperationName, Change
```

##### Identify non-compliant devices and retrieves incident-related information
```KQL
IntuneDeviceComplianceOrg
| where isnotempty(DeviceHealthThreatLevel)
| where ComplianceState != "Compliant"
| project TimeGenerated, ComplianceState, DeviceName, DeviceId, OS, UserName, UserEmail
| summarize arg_max(TimeGenerated, *) by DeviceId
```

