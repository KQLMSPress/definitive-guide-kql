## Bins, Percentages, and Percentiles

#### KQL queries from the Bins, Percentages, and Percentiles section of chapter 2 are found below

#### Use the copy option (to the right of each code box) to copy the query to paste into your own environment

#### Some queries use example data, or are based on hyopthetical scenarios, and you should switch in relevant data to your environment if required


##### Grouping data by day
```KQL
SigninLogs
| where  TimeGenerated > ago(14d)
| where ResultType == 0
| summarize SuccessfullSignIn=count() by bin(TimeGenerated, 1d)
| sort  by  TimeGenerated asc
```

##### Grouping data by day
```KQL
 AppPerformanceCounters
| where  TimeGenerated > ago(14d)
| where Name == "Requests/Sec" and AppRoleName == "Fabrikam-App"
| summarize sum(Value) by AppRoleName, Name, bin (TimeGenerated, 1d)
| project TimeGenerated, AppRoleName, Name, sum_Value
| sort by TimeGenerated asc
```

##### MSFT Employee Contribution-Laura Hutchcroft
##### Monitoring network traffic flows. This query looks at network flows per hour for the last 24 hours. Look for patterns and suspicious or long running network flows. Please see https://aka.ms/KQLMSPress/NetFlows for setup requirements.
```KQL
AzureNetworkAnalytics_CL
| where TimeGenerated > ago(24h)
| summarize sum(InboundFlows_d), sum(OutboundFlows_d) by bin(TimeGenerated, 1h)
```

##### Calculating percentage of MFA
```KQL
SigninLogs
| where TimeGenerated > ago (14d)
| where ResultType == 0
| project TimeGenerated, AppDisplayName, UserPrincipalName, ResultType, ResultDescription,AuthenticationRequirement, Location
| summarize TotalCount=count(),MultiFactor=countif(AuthenticationRequirement == "multiFactorAuthentication"), SingleFactor=countif(AuthenticationRequirement == "singleFactorAuthentication")
| extend ['MFA Percentage']=(todouble(MultiFactor) * 100 / todouble(TotalCount))
| extend ['SFA Percentage']=(todouble(SingleFactor) * 100 / todouble(TotalCount))
```

##### Using the round function
```KQL
SigninLogs
| where TimeGenerated > ago (14d)
| where ResultType == 0
| project TimeGenerated, AppDisplayName, UserPrincipalName, ResultType, ResultDescription,AuthenticationRequirement, Location
| summarize TotalCount=count(),MultiFactor=countif(AuthenticationRequirement == "multiFactorAuthentication"), SingleFactor=countif(AuthenticationRequirement == "singleFactorAuthentication")
| extend ['MFA Percentage']=round((todouble(MultiFactor) * 100 / todouble(TotalCount)), 2)
| extend ['SFA Percentage']=round((todouble(SingleFactor) * 100 / todouble(TotalCount)), 3)
```

##### Using the percentiles function
```KQL
AppPerformanceCounters
| where  TimeGenerated > ago(14d)
| where Name == "Available Bytes"
| summarize percentile(Value,50) by AppRoleName, Name
```

##### Using the percentiles function for multiple percents
```KQL
AppPerformanceCounters
| where  TimeGenerated > ago(14d)
| where Name == "Available Bytes"
| summarize percentiles(Value,25,50, 75) by AppRoleName, Name 
```