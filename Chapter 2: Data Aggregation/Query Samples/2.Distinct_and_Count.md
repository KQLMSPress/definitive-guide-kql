## Distinct and Count

#### KQL queries from the Distinct and Count section of chapter 2 are found below

#### Use the copy option (to the right of each code box) to copy the query to paste into your own environment

#### Some queries use example data, or are based on hyopthetical scenarios, and you should switch in relevant data to your environment if required


##### Looking at user agent values
```KQL
SigninLogs
| where TimeGenerated > ago (14d)
| project UserAgent
```

##### Using the distinct operator
```KQL
SigninLogs
| where TimeGenerated > ago (14d)
| distinct UserAgent
```

##### Using the distinct operator multiple columns
```KQL
SigninLogs
| where TimeGenerated > ago (14d)
| distinct AppDisplayName, UserAgent
| sort by AppDisplayName asc
```

##### Using the summarize operator
```KQL
SigninLogs
| where TimeGenerated > ago (14d)
| project TimeGenerated, UserAgent, AppDisplayName
| summarize take_any(*)
```

##### MSFT Employee Contribution-Michael Barbush
##### Applications using auto proxy. This query is useful for operational and security teams alike. If you donâ€™t know which applications are currently leveraging WPAD this will help you start to build that list. If you do know and see suspicious names or unexpected applications, these should be further investigated. 
```KQL
//Change timeframe to fit needs
DeviceNetworkEvents
| where RemoteUrl has 'wpad' and Timestamp > ago(1h)
| summarize by InitiatingProcessFileName, InitiatingProcessVersionInfoProductName, RemoteUrl, ActionType
| sort by InitiatingProcessFileName asc
```

##### Using the summarize operator with count function
```KQL
SigninLogs
| where TimeGenerated > ago (14d)
| summarize count() by UserAgent
```

##### Using the summarize operator with count function
```KQL
SigninLogs
| where TimeGenerated > ago (14d)
| summarize UserAgentCount = count() by UserAgent
| sort by UserAgentCount asc
```

##### Using the summarize operator with count function multiple columns
```KQL
SigninLogs
| where TimeGenerated > ago (14d)
| summarize UserAgentCount = count() by UserAgent, AppDisplayName
| sort by UserAgent desc
```

##### MSFT Employee Contribution-Kristopher Bash
##### Exploring client info for a particular application. This query summarizes the count of API requests to Microsoft Graph APIs for a specific application, with metadata about the clients, such as IP Address and UserAgent strings. This can be useful to understand more about deployment and use of a specific application in your tenant. The Location field reflects the region of the Microsoft Graph service that serves the request. This is typically the closest region to the client. 
```KQL
MicrosoftGraphActivityLogs
| where TimeGenerated > ago(3d)
| where AppId =='e9134e10-fea8-4167-a8d0-94c0e715bcea'
| summarize RequestCount=count() by  Location, IPAddress, UserAgent
```

##### Using the summarize operator with count function multiple columns
```KQL
SigninLogs
| where TimeGenerated > ago (14d)
| summarize AppDisplayNameCount = count() by AppDisplayName, UserAgent
| sort by AppDisplayNameCount desc
```

##### Using the summarize operator with the dcount function
```KQL
SigninLogs
| where TimeGenerated > ago (14d)
| summarize AppDisplayNameCount = dcount(AppDisplayName) by UserAgent
| sort by AppDisplayNameCount desc
```

##### MSFT Employee Contribution-Jef Kazimer
##### Delving into the identity governance operations. These queries offers critical insights into activities necessitating further scrutiny. This suite of queries is designed to enumerate operations linked to pivotal identity governance features, thereby illuminating the extent of Identity Governance and Administration (IGA) activities. It aims to enhance administrator awareness regarding configuration modifications, as well as end-user actions including access requests, approvals, and subsequent assignments. Further exploration of specific operations provides a deeper understanding of the access governance state, showcasing the efficiency of implemented access control measures.  Note: Ensure your query time range includes as much history that you have enabled for retention in your log analytics workspace.
```KQL
AuditLogs
| where LoggedByService == "Entitlement Management"
| summarize OperationCount = count() by OperationName, AADOperationType
| order by OperationCount desc
```

```KQL
AuditLogs
| where LoggedByService == "Access Reviews"
| summarize OperationCount = count() by OperationName, AADOperationType
| order by OperationCount desc
```

```KQL
AuditLogs
| where LoggedByService == "Lifecycle Workflows"
| summarize OperationCount = count() by OperationName, AADOperationType
| order by OperationCount desc
```

```KQL
AuditLogs
| where LoggedByService == "PIM"
| summarize OperationCount = count() by OperationName, AADOperationType
| order by OperationCount desc
```

##### Using the summarize operator with the dcount function
```KQL
SigninLogs
| where TimeGenerated > ago (14d)
| summarize UserAgentCount = dcount(UserAgent) by AppDisplayName
| sort by UserAgentCount desc
```

##### Using the summarize operator with the countif function
```KQL
SigninLogs
| where TimeGenerated > ago(14d)
| summarize TotalCount = count(), USLogins=countif(Location == "US") by AppDisplayName
| sort by USLogins desc
```

##### MSFT Employee Contribution-Mark Hopper
##### Intune Insights. These queries can help you get a sense of what is happening with your devices in Intune. The first query will show you the count of successful crate, delete and patch events for the last seven days. The second will provide a view of how many device enroll ment success and failures broken out by operating system type. Looking for patterns and changes can help indicate something is not working as expected.
```KQL
IntuneAuditLogs
| where TimeGenerated > ago(7d)
| where ResultType == "Success"
| where OperationName has_any ("Create", "Delete", "Patch")
| summarize Operations=count() by OperationName, Identity
| sort by Operations, Identity
```

```KQL
IntuneOperationalLogs 
| where OperationName == "Enrollment" 
| extend PropertiesJson = todynamic(Properties)
| extend OS = tostring(PropertiesJson["Os"]) 
| extend EnrollmentTimeUTC = todatetime(PropertiesJson["EnrollmentTimeUTC"])
| extend EnrollmentType = tostring(PropertiesJson["EnrollmentType"])
| project OS, Date = format_datetime(EnrollmentTimeUTC, 'M-d-yyyy'), Result
| summarize 
    iOS_Successful_Enrollments = countif(Result == "Success" and OS == "iOS"), 
    iOS_Failed_Enrollments = countif(Result == "Fail" and OS == "iOS"), 
    Android_Successful_Enrollmenst = countif(Result == "Success" and OS == "Android"),
    Android_Failed_Enrollments = countif(Result == "Fail" and OS == "Android"),
    Windows_Succesful_Enrollments = countif(Result == "Success" and OS == "Windows"),
    Windows_Failed_Enrollments = countif(Result == "Fail" and OS == "Windows")
    by Date
```

##### Using the summarize operator with the dcountif function
```KQL
SigninLogs
| where TimeGenerated > ago(14d)
| summarize TotalCount = count(), USUserAgent=dcountif(UserAgent, Location == "US") by AppDisplayName
| where USUserAgent > 0
| sort by USUserAgent desc
```