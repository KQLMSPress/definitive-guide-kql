## Visualizing Data with the Render Operator

#### KQL queries from the Visualizing Data with the Render Operator section of chapter 2 are found below

#### Use the copy option (to the right of each code box) to copy the query to paste into your own environment

#### Some queries use example data, or are based on hyopthetical scenarios, and you should switch in relevant data to your environment if required


##### Creating a pie chart
```KQL
SigninLogs
| where TimeGenerated > ago(14d)
| where ResultType == "0"
| summarize Appcount = count() by AppDisplayName
| render piechart
```

##### MSFT Employee Contribution-Krishna Venkit
##### Conditional access monitoring. These queries are useful to ensure that your conditional access policies are applying as expected. The first query will give you which applications that have sign-ins where there isnâ€™t a conditional access policy applied. The second query will show which applications have the most conditional access failures, either the policy was not satisfied or access was blocked.
```KQL
SigninLogs
| where TimeGenerated > ago(1d)
| project ConditionalAccessStatus, AppDisplayName
| where ConditionalAccessStatus has "notapplied"
| summarize count() by AppDisplayName
| render piechart
```

```KQL
SigninLogs
| where TimeGenerated > ago(1d)
| project ConditionalAccessStatus, AppDisplayName
| where ConditionalAccessStatus has "failure"
| summarize count() by AppDisplayName
| render piechart
```

##### Creating a bar chart
```KQL
SigninLogs
| where TimeGenerated > ago(14d)
| where ResultType == "0"
| summarize Appcount = count() by AppDisplayName
| limit 10
| render barchart
```

##### Creating a column chart
```KQL
SigninLogs
| where TimeGenerated > ago(14d)
| where ResultType == "0"
| summarize Signcount = count() by AppDisplayName, bin(TimeGenerated, 1d)
| render columnchart
```

##### Creating a column chart unstacked
```KQL
SigninLogs
| where TimeGenerated > ago(14d)
| where ResultType == "0"
| summarize Signcount = count() by AppDisplayName, bin(TimeGenerated, 1d)
| render columnchart with (kind=unstacked)
```

##### MSFT Employee Contribution-Estefani Arroyo
##### Cosmos DB Consumption. This query can help you determing the Cosmos DB request unity consumption by the physical partition, across all replicas in the replica set. If consumption is skewed among their partitions, you may want to consider remodeling your data and chose a partition key with a higher cardinality.
```KQL
CDBPartitionKeyRUConsumption 
| where TimeGenerated >= now(-1d) 
//specify collection and database 
//| where DatabaseName == "DBNAME" and CollectionName == "COLLECTIONNAME" 
// filter by operation type 
//| where operationType_s == 'Create' 
| summarize sum(todouble(RequestCharge)) by toint(PartitionKeyRangeId) 
| render columnchart 
```

##### Creating a time chart
```KQL
SigninLogs
| where TimeGenerated > ago(30d)
| where ResultType == "0"
| summarize Signcount = count() by bin(TimeGenerated, 1d)
| render timechart
```

##### MSFT Employee Contribution-Cosmin Guliman
##### Entra ID dynamic group processing changes. This query will help you track a dynamic group membership change processing taking place in your tenant for whatever group you specify. This can be helpful when making large changes to the dynamic group membership as well as normal day to day churn of the group.
```KQL
AuditLogs
| where Category == "GroupManagement"
| where TargetResources == "REPLACE" // group id you want to monitor
| where ActivityDisplayName in ("Add member to group","Remove member from group") or ActivityDisplayName =="Update group"
| summarize count() by TimeGenerated
| render timechart
```

##### Creating an area chart
```KQL
SigninLogs
| where TimeGenerated > ago(14d)
| where ResultType == "0"
| summarize Signcount = count() by AppDisplayName, bin(TimeGenerated, 1d)
| render areachart
```

##### Creating an area chart unstacked
```KQL
SigninLogs
| where TimeGenerated > ago(14d)
| where ResultType == "0"
| summarize Signcount = count() by AppDisplayName, bin(TimeGenerated, 1d)
| render areachart with (kind=unstacked)
```

##### Creating a time chart with data gaps
```KQL
SigninLogs
| where TimeGenerated > ago(30d)
| where ResultType == 0
| where AppDisplayName == "Azure Purview"
| summarize Count=count() by bin(TimeGenerated, 4h)
| render  timechart 
```

##### Using the make-series operator
```KQL
SigninLogs
| where TimeGenerated > ago(30d)
| where ResultType == 0
| where AppDisplayName == "Azure Purview"
| make-series Count=count() default=0 on TimeGenerated step 4h
| render timechart
```

##### Using the make-series operator with trend line
```KQL
SigninLogs
| where TimeGenerated > ago(30d)
| where ResultType != "53003"
| make-series Count=count() default=0 on TimeGenerated step 1d
| extend (RSquare, SplitIdx, Variance, RVariance, TrendLine)=series_fit_2lines(Count)
| project TimeGenerated, Count, TrendLine
| render timechart with (xtitle="Day", ytitle="Failure Count", title="Conditional access failures with trend over time")
```

##### MSFT Employee Contribution-Kristopher Bash
##### Exploring traffic patterns by time of day. This query will use the timestamp(TimeGenerated) to understand the traffic patterns in your tenant to Microsoft Graph APIs.
```KQL
MicrosoftGraphActivityLogs
| where TimeGenerated  between (ago(3d) .. ago(1h)) 
| summarize EventCount = count() by bin(TimeGenerated, 10m)
| render timechart
    with (
    title="Recent traffic patterns",
    xtitle="Time",
    ytitle="Requests",
    legend=hidden
    )
```
