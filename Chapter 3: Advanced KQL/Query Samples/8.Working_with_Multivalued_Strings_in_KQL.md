## Working with Multivalued Strings in KQL

#### KQL queries from the Working with Multivalued Strings in KQL section of chapter 3 are found below

#### Use the copy option (to the right of each code box) to copy the query to paste into your own environment

#### Some queries use example data, or are based on hyopthetical scenarios, and you should switch in relevant data to your environment if required


##### mv-expand Operator Syntax
```KQL
T | mv-expand [bagexpansion=(bag|array)] [with_itemindex=IndexColumnName] ColumnName [to typeof(Typename)] [, ColumnName ...] [limit Rowlimit]
```

##### Single column - array expansion
```KQL
datatable (a: int, b: dynamic)
[
    1, dynamic([10, 20]),
    2, dynamic(['a', 'b'])
]
| mv-expand b
```

##### Single column - bag expansion
```KQL
datatable (a: int, b: dynamic)
[
    1, dynamic({"prop1": "a1", "prop2": "b1"}),
    2, dynamic({"prop1": "a2", "prop2": "b2"})
]
| mv-expand b

Output:
a b
1 {"prop1": "a1"}
1 {"prop2": "b1"}
2 {"prop1": "a2"}
2 {"prop2": "b2"}
```

##### Single column - bag expansion to key-value pairs
```KQL
datatable (a: int, b: dynamic)
[
    1, dynamic({"prop1": "a1", "prop2": "b1"}),
    2, dynamic({"prop1": "a2", "prop2": "b2"})
]
| mv-expand bagexpansion=array b
| extend key = b[0], val = b[1]
```

##### Zipped two columns
```KQL
datatable (a: int, b: dynamic, c: dynamic)
[
    1, dynamic({"prop1": "a", "prop2": "b"}), dynamic([5, 4, 3])
]
| mv-expand b, c
```

##### Cartesian product of two columns
```KQL
datatable (a: int, b: dynamic, c: dynamic)
[
    1, dynamic({"prop1": "a", "prop2": "b"}), dynamic([5, 6])
]
| mv-expand b
| mv-expand c
```

##### Convert output
```KQL
datatable (a: string, b: dynamic, c: dynamic)
[
    "Constant", dynamic([1, 2, 3, 4]), dynamic([6, 7, 8, 9])
]
| mv-expand b, c to typeof(int)
| getschema
```

##### Extracting data from a well-formatted string
```KQL
datatable (Name: string)
[
    "GET /api/users",
    "GET /api/products",
    "GET /api/orders"
]
| parse Name with "GET " Data
```

##### Extracting multiple parts from a string
```KQL
datatable (Message: string)
[
    "Executed 'Function2' (Failed, Id=123, Duration=500ms)",
    "Executed 'Function2' (Failed, Id=456, Duration=750ms)"
]
| parse Message with "Executed 'Function2' (Failed, Id=" ID ", Duration=" Duration "ms)"
```

##### MSFT Employee Contribution-Corissa Koopmans & Chad Cox
##### Here are two queries that really demonstrate the power of mv-expand and parse. The first query allows you to determine which administrative activity was performed if that administrator role had any risk associated with it. The second query displays any changes to the most used applications which if suspicious might be an indication of an attack or malicious activity.
```KQL
let privroles = pack_array("Application Administrator","Authentication Administrator","Cloud Application Administrator","Conditional Access Administrator","Exchange Administrator","Global Administrator","Helpdesk Administrator","Hybrid Identity Administrator","Password Administrator","Privileged Authentication Administrator","Privileged Role Administrator","Security Administrator","SharePoint Administrator","User Administrator");
let privusers = AuditLogs 
| where TimeGenerated > ago(60d) and ActivityDisplayName == 'Add member to role completed (PIM activation)' and Category == "RoleManagement" 
| extend Caller = tostring(InitiatedBy.user.userPrincipalName) 
| extend Role = tostring(TargetResources[0].displayName) 
| where Role in (privroles) 
| distinct Caller;
let Activity =  AuditLogs
    | mv-expand ParsedFields = parse_json(TargetResources)
    | extend Target = tostring(ParsedFields.userPrincipalName), DisplayName = tostring(ParsedFields.displayName)
    | project TimeGenerated, Target, DisplayName, ParsedFields, OperationName;
    let RiskyUsers = SigninLogs
    | where RiskLevelDuringSignIn == "high"
    | where RiskState == "atRisk"
    | project TimeGenerated,UserPrincipalName, UserDisplayName, RiskDetail, RiskLevelDuringSignIn, RiskState;
    Activity
    | join kind=inner(RiskyUsers) on $left.DisplayName==$right.UserDisplayName
    | where TimeGenerated >= ago(7d) and UserPrincipalName in~ (privusers)
    | distinct UserDisplayName, RiskDetail, RiskLevelDuringSignIn, OperationName
```

```KQL
let MostUsedApps = SigninLogs
    | where TimeGenerated > ago(30d)
    | summarize dcount(CorrelationId) by AppId, AppDisplayName
    | top 100 by dcount_CorrelationId;
    //| summarize TopApps = make_list(AppId);
let Activty = AuditLogs
//| where OperationName has "application"
| mv-expand ParsedFields = parse_json(TargetResources)
| extend TargetId = tostring(ParsedFields.id)
| extend TargetName = tostring(ParsedFields.displayName)
| project TargetId, TargetName, OperationName, ActivityDisplayName;
MostUsedApps
| join kind = inner(Activty) on $left.AppId==$right.TargetId
| where isnotempty(TargetId)
| project AppId, AppDisplayName, ActivityDisplayName, OperationName
```
