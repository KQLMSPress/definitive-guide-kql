## Time Series Analysis

#### KQL queries from the Time Series Analysis section of chapter 3 are found below

#### Use the copy option (to the right of each code box) to copy the query to paste into your own environment

#### Some queries use example data, or are based on hyopthetical scenarios, and you should switch in relevant data to your environment if required


##### Traffic count partitioned by the operating system
```KQL
let min_t = toscalar(Perf | summarize min(TimeGenerated));
let max_t = toscalar(Perf | summarize max(TimeGenerated));
Perf
| make-series num=count() default=0 on TimeGenerated from min_t to max_t step 1h by ObjectName
| render timechart  
```

##### MSFT Employee Contribution-Keith Brewer
##### Tracking and visualizing Microsoft Entra ID authentication methods. This query enables you to visualize authentication method use over time during a registration campaign. This can also be placed in a workbook for a dashboard view.
```KQL
SigninLogs
| where ResultType == 0 or ResultType == 50074
// Filter out the AADC Sync Account
| where SignInIdentifier !startswith "Sync_"
// Filter out Sign-in Events from ADFS Connect Health
| where SourceSystem == "Azure AD"
| extend AuthenticationDetails = todynamic(AuthenticationDetails)
| mv-expand AuthenticationDetails
| extend authenticationMethod_ = tostring(parse_json(AuthenticationDetails).authenticationMethod)
// Filter out sign-in events without relevant Authentication Method Detail
| where authenticationMethod_ != "Previously satisfied" and authenticationMethod_ != ""
| make-series SignIns = count() default = 0 on TimeGenerated step 1d by authenticationMethod_
```

##### A moving average filter to the time series
```KQL
let min_t = toscalar(Perf | summarize min(TimeGenerated));
let max_t = toscalar(Perf | summarize max(TimeGenerated));
Perf
| make-series num=count() default=0 on TimeGenerated from min_t to max_t step 1h by ObjectName
| extend ma_num=series_fir(num, repeat(1, 5), true, true)
| render timechart
```

