## Using the Externaldata KQL Operator

#### KQL queries from the Using the Externaldata KQL Operator section of chapter 3 are found below

#### Use the copy option (to the right of each code box) to copy the query to paste into your own environment

#### Some queries use example data, or are based on hyopthetical scenarios, and you should switch in relevant data to your environment if required


##### Basic Use Case: Analyzing Processor Utilization
```KQL
InsightsMetrics
| where TimeGenerated > ago(30m)
| where Origin == "vm.azm.ms"
| where Namespace == "Processor"
| where Name == "UtilizationPercentage"
| summarize avg(Val) by bin(TimeGenerated, 5m), Computer
| join kind=leftouter (ComputerGroup) on Computer
| where isnotempty(Computer1)
| sort by avg_Val desc nulls first 
```

##### Enhanced Use Case: Dynamic Thresholds for Processor Utilization
```KQL
let Thresholds = externaldata (Computer: string, Threshold: int)
[@"https://raw.githubusercontent.com/KQLMSPress/definitive-guide-kql/main/Chapter%203%3A%20Advanced%20KQL/Data%20Samples/thresholds.csv"]
with (format="csv");
InsightsMetrics
| where TimeGenerated > ago(30m)
| where Origin == "vm.azm.ms"
| where Namespace == "Processor"
| where Name == "UtilizationPercentage"
| join kind=inner (Thresholds) on Computer
| where Val > Threshold
| sort by Val desc nulls first  
```

