## Advanced KQL for Cyber Security 

#### KQL queries from the time operators section of Advanced KQL for Cyber Security are found here

#### Use the copy option (to the right of each code box) to copy the query to paste into your own environment

#### Some queries use example data, or are based on hyopthetical scenarios, and you should switch in relevant data to your environment if required

### Example sign in data to use with time operators
```KQL
SigninLogs
| project TimeGenerated, UserPrincipalName, AppDisplayName, ResultType, Location
```

### Extending additional timezones
```KQL
SigninLogs
| project TimeGenerated, UserPrincipalName, AppDisplayName, ResultType, Location
| extend EST=TimeGenerated-5h
| extend JST=TimeGenerated+9h
```

### Extending timezones using friendly names
```KQL
SigninLogs
| extend SydneyTime=datetime_utc_to_local(TimeGenerated,'Australia/Sydney')
```

### Example sign in logs using startofday()
```KQL
SigninLogs
| where TimeGenerated > startofday(now())
```

### Using days and hours of the day to find specific events
```KQL
let Sunday = time(0.00:00:00);
let Saturday = time(6.00:00:00);
SigninLogs
| where dayofweek(TimeGenerated) in (Saturday, Sunday) or hourofday(TimeGenerated) !between (6 .. 18)
| where UserPrincipalName startswith "adm" and RiskLevelDuringSignIn in ("medium","high")
| project TimeGenerated, UserPrincipalName, ResultType, AppDisplayName, IPAddress, UserAgent
```

### Finding events close together based on time
```KQL
SigninLogs
| where TimeGenerated > ago (7d)
| where RiskLevelDuringSignIn in ("medium", "high")
| where ResultType in ("50079","50072")
| project RiskTime=TimeGenerated, UserPrincipalName, IPAddress, Location, ResultType, ResultDescription
| join kind=inner(
    AuditLogs
    | where TimeGenerated > ago (7d)
    | where OperationName == "User registered security info"
    | where Result == "success"
    | extend UserPrincipalName = tostring(TargetResources[0].userPrincipalName)
    )
    on UserPrincipalName
| project-rename MFATime=TimeGenerated, MFAResult=ResultDescription1
| where (MFATime - RiskTime) between (0min .. 30m)
| extend TimeDelta=MFATime-RiskTime
| project RiskTime, MFATime, TimeDelta, UserPrincipalName, IPAddress, Location, ResultType, ResultDescription, MFAResult
```

### Example data to use for additional time operators
```KQL
DeviceNetworkEvents
| where TimeGenerated > ago(1d)
| sample 100
| sort by TimeGenerated asc  
```

### Calculating the time between events using datetime_diff and prev()
```KQL
DeviceNetworkEvents
| where TimeGenerated > ago(1d)
| sample 100
| sort by TimeGenerated asc  
| extend TimeDiffInMinutes=datetime_diff('minute',TimeGenerated, prev(TimeGenerated,1))
```

### Calculating the time between events using datetime_diff and next()
```KQL
DeviceNetworkEvents
| where TimeGenerated > ago(1d)
| sample 100
| sort by TimeGenerated asc  
| extend TimeDiffInMinutes=datetime_diff('minute',TimeGenerated, next(TimeGenerated,1))
```
