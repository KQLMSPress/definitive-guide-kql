## Advanced KQL for Cyber Security 

#### KQL queries from the let section of Advanced KQL for Cyber Security are found here

#### Use the copy option (to the right of each code box) to copy the query to paste into your own environment

#### Some queries use example data, or are based on hyopthetical scenarios, and you should switch in relevant data to your environment if required

### Casting an IP as a variable
```KQL
let ip="50.50.50.50";
SigninLogs
| where IPAddress == ip
```

### Casting a query as a variable - this query is purposely imcomplete, and will error
```KQL
let riskyusers=
SigninLogs
| where RiskLevelDuringSignIn == "high"
| distinct UserPrincipalName;
```

### Reusing our variable in our query
```KQL
let riskyusers=
SigninLogs
| where RiskLevelDuringSignIn == "high"
| distinct UserPrincipalName;
AuditLogs
| where TimeGenerated > ago (1d)
| where OperationName in ("User registered security info", "User deleted security info","User registered all required security info")
| where Result == "success"
| extend UserPrincipalName = tostring(TargetResources[0].userPrincipalName)
| where UserPrincipalName in (riskyusers)
```

### Using multiple nested let statements
```KQL
let riskyusers=
SigninLogs
| where RiskLevelDuringSignIn == "high"
| distinct UserPrincipalName;
let riskyips=
SigninLogs
| where RiskLevelDuringSignIn == "high"
| distinct IPAddress;
let mfausers=
AuditLogs
| where TimeGenerated > ago (1d)
| where OperationName in ("User registered security info", "User deleted security info","User registered all required security info")
| where Result == "success"
| extend UserPrincipalName = tostring(TargetResources[0].userPrincipalName)
| where UserPrincipalName in (riskyusers)
| distinct UserPrincipalName;
CloudAppEvents
| where ActionType == "FileDownloaded"
| extend IPAddress = tostring(RawEventData.ClientIP)
| extend FileName = tostring(RawEventData.ObjectId)
| extend UserPrinciplName = tostring(RawEventData.UserId)
| where UserPrinciplName in (mfausers) or IPAddress in (riskyips)
```

### Excluding variables using let statements
```KQL
let corpips=
SigninLogs
| where NetworkLocationDetails has "Corporate IPs"
| distinct IPAddress;
let riskyusers=
SigninLogs
| where RiskLevelDuringSignIn == "high"
| distinct UserPrincipalName;
let riskyips=
SigninLogs
| where RiskLevelDuringSignIn == "high"
| distinct IPAddress;
let mfausers=
AuditLogs
| where TimeGenerated > ago (1d)
| where OperationName in ("User registered security info", "User deleted security info","User registered all required security info")
| where Result == "success"
| extend UserPrincipalName = tostring(TargetResources[0].userPrincipalName)
| where UserPrincipalName in (riskyusers)
| distinct UserPrincipalName;
CloudAppEvents
| where ActionType == "FileDownloaded"
| extend IPAddress = tostring(RawEventData.ClientIP)
| extend FileName = tostring(RawEventData.ObjectId)
| extend UserPrinciplName = tostring(RawEventData.UserId)
| where UserPrinciplName in (mfausers) or IPAddress in (riskyips)
| where IPAddress !in (corpips)
```
