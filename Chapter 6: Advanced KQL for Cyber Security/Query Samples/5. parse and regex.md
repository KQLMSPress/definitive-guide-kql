## Advanced KQL for Cyber Security 

#### KQL queries from the parse and regex section of Advanced KQL for Cyber Security are found here

#### Use the copy option (to the right of each code box) to copy the query to paste into your own environment

#### Some queries use example data, or are based on hyopthetical scenarios, and you should switch in relevant data to your environment if required

### Data to use with parse-where
```KQL
datatable (data:string) [
"srcipaddr=10.10.10.10,dstipaddr=50.50.50.50,srcport=25,dstport=443,protocol=tcp",
"srcipaddr=10.10.10.10,dstipaddr=50.50.50.50,srcport=25,dstport=443,protocol=tcp",
"sourceip=10.10.10.10,destinationip=50.50.50.50,sourceport=25,destport=443,trafficprotocol=tcp"]
```

### Example using regular parse
```KQL
datatable (data:string) [
"srcipaddr=10.10.10.10,dstipaddr=50.50.50.50,srcport=25,dstport=443,protocol=tcp",
"srcipaddr=10.10.10.10,dstipaddr=50.50.50.50,srcport=25,dstport=443,protocol=tcp",
"sourceip=10.10.10.10,destinationip=50.50.50.50,sourceport=25,destport=443,trafficprotocol=tcp"]
| parse data with * @"srcipaddr=" SourceIP @",dstipaddr=" DestinationIP @",srcport=" SourcePort @",dstport=" DestinationPort @",protocol=" Protocol
```

### Example using parse-where
```KQL
datatable (data:string) [
"srcipaddr=10.10.10.10,dstipaddr=50.50.50.50,srcport=25,dstport=443,protocol=tcp",
"srcipaddr=10.10.10.10,dstipaddr=50.50.50.50,srcport=25,dstport=443,protocol=tcp",
"sourceip=10.10.10.10,destinationip=50.50.50.50,sourceport=25,destport=443,trafficprotocol=tcp"]
| parse-where data with * @"srcipaddr=" SourceIP @",dstipaddr=" DestinationIP @",srcport=" SourcePort @",dstport=" DestinationPort @",protocol=" Protocol
```

### Example data to use with parse_json
```KQL
datatable(Username:string,ErrorCode:string,LocationData:string) [
"eric.lang@tailspintoys.com","50126",'{"City":"London","Country":"UK","Lat":"51.5072N","Long":"0.1276W"}',
"kari.hensien@tailspintoys.com","0",'{"City":"Sydney","Country":"AU","Lat":"33.8688S","Long":"151.2093E"}',
"carole.poland@tailspintoys.com","50053",'{"City":"London","Country":"UK","Lat":"38.9072N","Long":"77.0369W"}'
]
```

### Using parse_json to extend columns
```KQL
datatable(Username:string,ErrorCode:string,LocationData:string) [
"eric.lang@tailspintoys.com","50126",'{"City":"London","Country":"UK","Lat":"51.5072N","Long":"0.1276W"}',
"kari.hensien@tailspintoys.com","0",'{"City":"Sydney","Country":"AU","Lat":"33.8688S","Long":"151.2093E"}',
"carole.poland@tailspintoys.com","50053",'{"City":"Washington DC","Country":"US","Lat":"38.9072N","Long":"77.0369W"}'
]
| extend LocationData=parse_json(LocationData)
| extend City=LocationData.City
```

##### MSFT Employee Contribution-Rudnei Oliveira
##### Parsing JSON can let you retrieve things like resource names from Azure Activity Logs. For instance, you can identify if someone creates a storage account to store and maintain malicious files within it.
```KQL
 AzureActivity 
| where CategoryValue == "Administrative" 
| where OperationNameValue contains "MICROSOFT.STORAGE/STORAGEACCOUNTS/WRITE" 
| where ResourceGroup !contains "CLOUD-SHELL-STORAGE" 
| where ActivityStatusValue == "Success" 
| extend storageaccname = tostring(parse_json(Properties).resource) 
| project Caller, OperationNameValue, CallerIpAddress, ResourceGroup, storageaccname 
```

### Looking at user agents in sign in logs
```KQL
SigninLogs
| where TimeGenerated > ago (30d)
| take 100
| distinct UserAgent
```

### Using parse_user_agent() to retrieve details
```KQL
SigninLogs
| where TimeGenerated > ago (30d)
| take 100
| distinct UserAgent
| extend UserAgentDetails=parse_user_agent(UserAgent,"browser")
```

### Looking at device events to find URLs
```KQL
DeviceEvents
| where ActionType == "BrowserLaunchedToOpenUrl" and RemoteUrl startswith "http"
| distinct RemoteUrl
```

### Using parse_url to retrieve details
```KQL
DeviceEvents
| distinct RemoteUrl
| extend UrlDetails=parse_url(RemoteUrl)
```

### Example data to use with regex
```KQL
datatable (data:string) [
"ipaddress=50.50.50.50-url=tailspintoys.com",
"ipaddress=50.50.50.50-username=eric@tailspintoys.com",
"ipaddress=50.50.50.50-userid=39372",
"ipaddress=unknown-userid=39281"
]
```

### Searching data with regex
```KQL
datatable (data:string) [
"ipaddress=50.50.50.50-url=tailspintoys.com",
"ipaddress=50.50.50.50-username=eric@tailspintoys.com",
"ipaddress=50.50.50.50-userid=39372",
"ipaddress=unknown-userid=39281"
]
| where data matches regex @"((?:[0-9]{1,3}\.){3}[0-9]{1,3})" 
```

### Searching data with not and regex
```KQL
datatable (data:string) [
"ipaddress=50.50.50.50-url=tailspintoys.com",
"ipaddress=50.50.50.50-username=eric@tailspintoys.com",
"ipaddress=50.50.50.50-userid=39372",
"ipaddress=unknown-userid=39281"
]
| where not (data matches regex @"([a-z0-9|-]+\.)*[a-z0-9|-]+\.[a-z]+")
```

### Example data to use with extract
```KQL
datatable (data:string) [
"ipaddress=50.50.50.50-url=tailspintoys.com,ipaddress=40.40.40.40-url=detective.kusto.io,ipaddress=60.60.60.60-url=aka.ms/kustofree"
]
```

### Using extract to retrieve an IP address
```KQL
datatable (data:string) [
"ipaddress=50.50.50.50-url=tailspintoys.com,ipaddress=40.40.40.40-url=detective.kusto.io,ipaddress=60.60.60.60-url=aka.ms/kustofree"
]
| extend IPAddress=extract(@"((?:[0-9]{1,3}\.){3}[0-9]{1,3})",0,data)
```

### Using extract_all to retrieve all IP addresses
```KQL
datatable (data:string) [
"ipaddress=50.50.50.50-url=tailspintoys.com,ipaddress=40.40.40.40-url=detective.kusto.io,ipaddress=60.60.60.60-url=aka.ms/kustofree"
]
| extend IPAddress=extract_all(@"((?:[0-9]{1,3}\.){3}[0-9]{1,3})",data)
```

##### MSFT Employee Contribution-Kristopher Bash
##### We may want to understand what applications in the tenant are using a specific API request. This query filters by aspects of the requestUri to summarize applications using a specific API. The RequestUri can be extracted, parsed, as illustrated in this example, or matched for substrings with the has operator.
```KQL
MicrosoftGraphActivityLogs 
| where TimeGenerated > ago(3d) 
| extend path = replace_string(replace_string(replace_regex(tostring(parse_url(RequestUri).Path), @'(\/)+','//'),'v1.0/',''),'beta/','') 
| extend UriSegments =  extract_all(@'\/([A-z2]+|\$batch)($|\/|\(|\$)',dynamic([1]),tolower(path)) 
| extend OperationResource = strcat_array(UriSegments,'/') 
| where OperationResource == 'oauth2permissiongrants' 
| summarize RequestCount=count() by AppId 
```
