## Advanced KQL for Cyber Security 

#### KQL queries from the misc and functions section of Advanced KQL for Cyber Security are found here

#### Use the copy option (to the right of each code box) to copy the query to paste into your own environment

#### Some queries use example data, or are based on hyopthetical scenarios, and you should switch in relevant data to your environment if required

### Hypothetical query for firewall data - you can use any data with IP information with these queries
```KQL
FirewallLogs
| where ipv4_is_private(IPAddress)
```

### Hypothetical query for firewall data - you can use any data with IP information with these queries. Query extends whether an IP is private or not.
```KQL
FirewallLogs
| extend PrivateIP=iff(ipv4_is_private(IPAddress),"true","false")
```

### Visualizing additional geolocation information
```KQL
datatable (SourceIPAddress:string,DestinationIPAddress:string,Port:string) [
"192.168.1.5","50.50.50.50","443",
"192.168.1.13","60.60.60.60","80",
"192.168.5.65","50.50.50.50","22",
"192.168.2.67","70.70.70.70","443",
] 
```

### Finding if IP addresses are in specific ranges
```KQL
datatable (SourceIPAddress:string,DestinationIPAddress:string,Port:string) [
"192.168.1.5","50.50.50.50","443",
"192.168.1.13","60.60.60.60","80",
"192.168.5.65","50.50.50.50","22",
"192.168.2.67","70.70.70.70","443",
]
| extend isVPN = ipv4_is_in_range(SourceIPAddress,'192.168.1.0/26')
```

### Finding if IP addresses are in one of many ranges
```KQL
datatable (SourceIPAddress:string,DestinationIPAddress:string,Port:string) [
"192.168.1.5","50.50.50.50","443",
"192.168.1.13","60.60.60.60","80",
"192.168.5.65","50.50.50.50","22",
"192.168.2.67","70.70.70.70","443",
]
| extend isVPN = ipv4_is_in_any_range(SourceIPAddress,'192.168.1.0/26','192.168.2.0/24')
```

### Sample data that is base64 encoded
```KQL
datatable (ProcessName:string,ProcessParams:string) [
"PowerShell.exe","VGhlIERlZmluaXRpdmUgR3VpZGUgdG8gS1FM",
"PowerShell.exe","SHVtYW4ga25vd2xlZGdlIGJlbG9uZ3MgdG8gdGhlIHdvcmxkIQ==",
"PowerShell.exe","aHR0cHM6Ly90d2l0dGVyLmNvbS9yZXByaXNlXzk5"
]
```

### Decoding base64 encoded strings
```KQL
datatable (ProcessName:string,ProcessParams:string) [
"PowerShell.exe","VGhlIERlZmluaXRpdmUgR3VpZGUgdG8gS1FM",
"PowerShell.exe","SHVtYW4ga25vd2xlZGdlIGJlbG9uZ3MgdG8gdGhlIHdvcmxkIQ==",
"PowerShell.exe","aHR0cHM6Ly90d2l0dGVyLmNvbS9yZXByaXNlXzk5"
]
| extend Decoded=base64_decode_tostring(ProcessParams)
```

### Using toscalar to visualize email traffic
```KQL
let AverageBlockedEmail = toscalar(EmailEvents
| where TimeGenerated > ago(30d)
| where DeliveryAction == "Blocked"
| summarize Count=count() by bin(TimeGenerated, 1d)
| summarize avg(Count));
EmailEvents
| where TimeGenerated > ago(30d)
| where DeliveryAction == "Blocked"
| summarize Count=count() by bin(TimeGenerated, 1d)
| extend Deviation = (Count - AverageBlockedEmail) / AverageBlockedEmail
| project-away Count
| render columnchart
```

### Creating a pivot table using sign in data
```KQL
SigninLogs
| where TimeGenerated > ago (30d)
| where ResultType == 0
| where AppDisplayName has "Azure"
| evaluate pivot(AppDisplayName,count(), UserPrincipalName)
```

### Example query to create a function from
```KQL
SigninLogs
| project TimeGenerated, UserPrincipalName, AppDisplayName, ResultType, IPAddress, Location, UserAgent, RiskLevelDuringSignIn
```

### Querying the newly saved function
```KQL
AADLogs
| where ResultType == "0" and RiskLevelDuringSignIn == "high" 
```

### Example firewall log to turn into a function
```KQL
externaldata (data:string)[h@'https://raw.githubusercontent.com/KQLMSPress/definitive-guide-kql/main/Chapter%205%3A%20KQL%20for%20Cyber%20Security/Data%20Samples/FirewallLogs.csv']with(format='csv',ignorefirstrecord=false)
```

### Firewall parser to save as a function
```KQL
let Logs=externaldata (data:string)[h@'https://raw.githubusercontent.com/KQLMSPress/definitive-guide-kql/main/Chapter%205%3A%20KQL%20for%20Cyber%20Security/Data%20Samples/FirewallLogs.csv']with(format='csv',ignorefirstrecord=false);
let one=
Logs
| where data has "srcdev=10.10.10.10"
| parse data with * @"srcdev=" SourceDevice @",date=" Date @",time=" Time @",action=" Action @",sourceip=" SourceIP @",dstip=" DestinationIP @",srcprt=" SourcePort @",dstprt=" DestinationPort @",xproto=" Protocol @",bytesin=" BytesIn @",bytesout=" BytesOut
| project-away data
| extend Month=tostring(split(Date," ")[0]), Day=tostring(split(Date," ")[1]),Year=tostring(split(Date," ")[2])
| extend Day=extract(@'^(\d+)',1,Day)
| extend Time=tostring(split(Time,"(")[0])
| extend Time=replace_string(Time,".",":")
| extend Timestamp=strcat(Day," ",Month," ",Year," ",Time)
| extend Timestamp=todatetime(Timestamp)
| extend Timestamp=Timestamp-5h
| project-away Date, Time, Day, Month, Year
| project-reorder Timestamp, SourceDevice, Action, SourceIP, SourcePort, DestinationIP, DestinationPort, Protocol, BytesIn, BytesOut
;
let two=
Logs
| where data has "device:10.10.10.30"
| parse data with @"device:" SourceDevice @",timestamp:" Timestamp @"z,policy:" Policy @",outcome:" Action @",src=" SourceIP @":" SourcePort @"/" Protocol @",dst=" DestinationIP @":" DestinationPort @",datain=" BytesIn @"bytes,dataout=" BytesOut "bytes,tz=" Timezone
| project-away data
| extend DestinationPort=tostring(split(DestinationPort,"/")[0])
| extend Timestamp=todatetime(Timestamp)
| extend Timestamp=Timestamp+4h
| project-away Timezone
| project-reorder Timestamp, SourceDevice, Action, SourceIP, SourcePort, DestinationIP, DestinationPort, Protocol, BytesIn, BytesOut, Policy;
let three=
Logs
| where data has "SourceDeviceIP:10.10.10.20" and data has "connection accepted"
| parse data with Timestamp @": connection " Action @": sent " BytesOut @" bytes to endpoint " DestinationIP @":" DestinationPort @" from " SourceIP @":" SourcePort @" (SourceDeviceIP:" SourceDevice ")"
| project-away data
| extend Timestamp=todatetime(Timestamp)
| project-reorder Timestamp, SourceDevice, Action, SourceIP, SourcePort, DestinationIP, DestinationPort, BytesOut;
let four=
Logs
| where data has "SourceDeviceIP:10.10.10.20" and data has "connection denied"
| parse data with Timestamp @": connection " Action @": " DestinationIP @":" DestinationPort @" connection rejected from " SourceIP @":" SourcePort @" (SourceDeviceIP:" SourceDevice ")"
| project-away data
| extend Timestamp=todatetime(Timestamp)
| project-reorder Timestamp, SourceDevice, Action, SourceIP, SourcePort, DestinationIP, DestinationPort;
union one,two,three,four
| extend Action=case(Action has_any ("allow","accept","accepted"), strcat="allow",
                     Action has_any ("deny", "denied", "drop"), strcat="drop",
                     "other")
```

### Querying the newly saved function
```KQL
FirewallLogs
| where SourceIP == "50.50.50.50" and Protocol == "tcp"
```
