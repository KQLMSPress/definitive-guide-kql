## Advanced KQL for Cyber Security 

#### KQL queries from the geolocation section of Advanced KQL for Cyber Security are found here

#### Use the copy option (to the right of each code box) to copy the query to paste into your own environment

#### Some queries use example data, or are based on hyopthetical scenarios, and you should switch in relevant data to your environment if required

### Sampling sign in logs for geolocation operators
```KQL
SigninLogs
| sample 100
```

### Visualizing geolocation data using render
```KQL
SigninLogs
| extend Lat=toreal(['LocationDetails']['geoCoordinates']['latitude'])
| extend Long=toreal(['LocationDetails']['geoCoordinates']['longitude'])
| summarize Count=count() by Long, Lat
| render scatterchart with (kind=map
```

### Sample data missing geolocation data
```KQL
DeviceLogonEvents
| where ActionType == "LogonSuccess"
| where LogonType == "RemoteInteractive"
| where RemoteIPType == "Public"
| project TimeGenerated, DeviceName, AccountName, RemoteIP
```

### Using geo_info_from_ip_address to retrieve geolocation information
```KQL
DeviceLogonEvents
| where ActionType == "LogonSuccess"
| where LogonType == "RemoteInteractive"
| where RemoteIPType == "Public"
| extend GeoInfo=geo_info_from_ip_address(RemoteIP)
| project TimeGenerated, DeviceName, AccountName, RemoteIP, GeoInfo
```

### Hunting on geolocation data
```KQL
DeviceLogonEvents
| where ActionType == "LogonSuccess"
| where LogonType == "RemoteInteractive"
| where RemoteIPType == "Public"
| extend GeoInfo=geo_info_from_ip_address(RemoteIP)
| extend City = tostring(GeoInfo.city)
| extend Country = tostring(GeoInfo.country)
| extend Latitude = tostring(GeoInfo.latitude)
| extend Longitude = tostring(GeoInfo.longitude)
| extend State = tostring(GeoInfo.state)
| where City == "New York"
| project TimeGenerated, DeviceName, AccountName, RemoteIP, GeoInfo, City, Country, Latitude, Longitude, State
```

### Visualizing additional geolocation information
```KQL
DeviceLogonEvents
| where ActionType == "LogonSuccess"
| where LogonType == "RemoteInteractive"
| where RemoteIPType == "Public"
| project TimeGenerated, DeviceName, AccountName, RemoteIP
| extend GeoInfo=geo_info_from_ip_address(RemoteIP)
| project TimeGenerated, DeviceName, AccountName, RemoteIP, GeoInfo
| extend Lat=toreal(['GeoInfo']['latitude'])
| extend Long=toreal(['GeoInfo']['longitude'])
| summarize Count=count() by Long, Lat
| render scatterchart with (kind=map)
```

### Hypothetical query for firewall data - you can use any data with IP information with these queries
```KQL
FirewallLogs
| where ipv4_is_private(IPAddress)
```

### Hypothetical query for firewall data - you can use any data with IP information with these queries. Query extends whether an IP is private or not.
```KQL
FirewallLogs
| extend PrivateIP=iff(ipv4_is_private(IPAddress),"true","false")
```

### Visualizing additional geolocation information
```KQL
datatable (SourceIPAddress:string,DestinationIPAddress:string,Port:string) [
"192.168.1.5","50.50.50.50","443",
"192.168.1.13","60.60.60.60","80",
"192.168.5.65","50.50.50.50","22",
"192.168.2.67","70.70.70.70","443",
] 
```
