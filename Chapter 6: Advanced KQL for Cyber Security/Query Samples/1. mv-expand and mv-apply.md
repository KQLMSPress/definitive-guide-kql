## Advanced KQL for Cyber Security 

#### KQL queries from the multivalued string section of Advanced KQL for Cyber Security are found here

#### Use the copy option (to the right of each code box) to copy the query to paste into your own environment

#### Some queries use example data, or are based on hyopthetical scenarios, and you should switch in relevant data to your environment if required


##### Sign in logs showing schema
```KQL
SigninLogs
| project TimeGenerated, UserPrincipalName, ResultType, IPAddress, AuthenticationRequirement, ConditionalAccessStatus, ConditionalAccessPolicies
```

##### Sample sign in log data to use for mv-expand and mv-apply
```KQL
externaldata (TimeGenerated:datetime  ,UserPrincipalName:string,ResultType:string,IPAddress:string,AuthenticationRequirement:string,ConditionalAccessStatus:string,ConditionalAccessPolicies:dynamic)[h@'https://raw.githubusercontent.com/KQLMSPress/definitive-guide-kql/main/Chapter%206%3A%20Advanced%20KQL%20for%20Cyber%20Security/Data%20Samples/AADSignInLogs.csv']with(format='csv',ignorefirstrecord=true)
```

##### Sign in logs showing failed CA events
```KQL
externaldata (TimeGenerated:datetime,UserPrincipalName:string,ResultType:string,IPAddress:string,AuthenticationRequirement:string,ConditionalAccessStatus:string,ConditionalAccessPolicies:dynamic)[h@'https://raw.githubusercontent.com/KQLMSPress/definitive-guide-kql/main/Chapter%206%3A%20Advanced%20KQL%20for%20Cyber%20Security/Data%20Samples/AADSignInLogs.csv']with(format='csv',ignorefirstrecord=true)
| where ConditionalAccessStatus == "failure" 
```

##### Sampling sign in logs using take
```KQL
externaldata (TimeGenerated:datetime,UserPrincipalName:string,ResultType:string,IPAddress:string,AuthenticationRequirement:string,ConditionalAccessStatus:string,ConditionalAccessPolicies:dynamic)[h@'https://raw.githubusercontent.com/KQLMSPress/definitive-guide-kql/main/Chapter%206%3A%20Advanced%20KQL%20for%20Cyber%20Security/Data%20Samples/AADSignInLogs.csv']with(format='csv',ignorefirstrecord=true)
| take 1
```

##### Sign in logs showing mv-expand one one log
```KQL
externaldata (TimeGenerated:datetime,UserPrincipalName:string,ResultType:string,IPAddress:string,AuthenticationRequirement:string,ConditionalAccessStatus:string,ConditionalAccessPolicies:dynamic)[h@'https://raw.githubusercontent.com/KQLMSPress/definitive-guide-kql/main/Chapter%206%3A%20Advanced%20KQL%20for%20Cyber%20Security/Data%20Samples/AADSignInLogs.csv']with(format='csv',ignorefirstrecord=true)
| take 1
| mv-expand ConditionalAccessPolicies
```

##### MSFT Employee Contribution-Corissa Koopmans
##### Using mv-expand can provide interesting detection capabilities. Users coming from multiple devices within a relatively short period of time can be an indicator of a malicious actor.
```KQL
SigninLogs 
| where TimeGenerated > ago(90d) 
| mv-expand ParsedFields = parse_json(AuthenticationDetails) 
| extend AuthMethod = ParsedFields.authenticationMethod 
| extend ParsedFields2 = parse_json(DeviceDetail) 
| extend DeviceID = tostring(ParsedFields2.deviceId) 
| extend ParsedFields3 = parse_json(Status) 
| extend SigninStatus = tostring(ParsedFields3.errorCode) 
| where AuthMethod != "Previously satisfied" 
| where isnotempty(DeviceID) 
| where SigninStatus == 0 
| summarize dcount(DeviceID) by UserDisplayName 
| order by dcount_DeviceID desc 
```

##### Showing failure events without mv-expand
```KQL
externaldata (TimeGenerated:datetime,UserPrincipalName:string,ResultType:string,IPAddress:string,AuthenticationRequirement:string,ConditionalAccessStatus:string,ConditionalAccessPolicies:dynamic)[h@'https://raw.githubusercontent.com/KQLMSPress/definitive-guide-kql/main/Chapter%206%3A%20Advanced%20KQL%20for%20Cyber%20Security/Data%20Samples/AADSignInLogs.csv']with(format='csv',ignorefirstrecord=true)
| extend CAResult = tostring(ConditionalAccessPolicies[0].result)
| where CAResult == "failure"
```

##### Showing failure events with mv-expand
```KQL
externaldata (TimeGenerated:datetime,UserPrincipalName:string,ResultType:string,IPAddress:string,AuthenticationRequirement:string,ConditionalAccessStatus:string,ConditionalAccessPolicies:dynamic)[h@'https://raw.githubusercontent.com/KQLMSPress/definitive-guide-kql/main/Chapter%206%3A%20Advanced%20KQL%20for%20Cyber%20Security/Data%20Samples/AADSignInLogs.csv']with(format='csv',ignorefirstrecord=true)
| mv-expand ConditionalAccessPolicies
| extend CAResult = tostring(ConditionalAccessPolicies.result)
| where CAResult == "failure"
```

##### MSFT Employee Contribution-Corissa Koopmans
##### Using mv-expand can provide interesting detection capabilities. Users coming from multiple devices within a relatively short period of time can be an indicator of a malicious actor.
```KQL
SigninLogs 
| where TimeGenerated > ago(90d) 
| mv-expand ParsedFields = parse_json(AuthenticationDetails) 
| extend AuthMethod = ParsedFields.authenticationMethod 
| extend ParsedFields2 = parse_json(DeviceDetail) 
| extend DeviceID = tostring(ParsedFields2.deviceId) 
| extend ParsedFields3 = parse_json(Status) 
| extend SigninStatus = tostring(ParsedFields3.errorCode) 
| where AuthMethod != "Previously satisfied" 
| where isnotempty(DeviceID) 
| where SigninStatus == 0 
| summarize dcount(DeviceID) by UserDisplayName 
| order by dcount_DeviceID desc 
```

##### MSFT Employee Contribution-Corissa Koopmans
##### You can additionally query only for a specific user of interest by casting that user as a variable at the top of your query.
```KQL
let user = "user ID here"; 
SigninLogs 
|where TimeGenerated > ago(90d) 
|where UserId == user 
| mv-expand ParsedFields = parse_json(AuthenticationDetails) 
| extend AuthMethod = ParsedFields.authenticationMethod 
| extend ParsedFields2 = parse_json(DeviceDetail) 
| extend DeviceID = tostring(ParsedFields2.deviceId) 
| extend DeviceName = tostring(ParsedFields2.displayName) 
| extend DeviceOS = tostring(ParsedFields2.operatingSystem) 
| extend DeviceBrowser = tostring(ParsedFields2.browser) 
| extend ParsedFields3 = parse_json(Status) 
| extend SigninStatus = tostring(ParsedFields3.errorCode) 
| where AuthMethod != "Previously satisfied" 
| where isnotempty(DeviceID) 
| where SigninStatus == 0 
| summarize count() by UserDisplayName, UserId, DeviceID, DeviceName, DeviceOS, DeviceBrowser, SigninStatus 
```

##### Failure events with mv-apply
```KQL
externaldata (TimeGenerated:datetime,UserPrincipalName:string,ResultType:string,IPAddress:string,AuthenticationRequirement:string,ConditionalAccessStatus:string,ConditionalAccessPolicies:dynamic)[h@'https://raw.githubusercontent.com/KQLMSPress/definitive-guide-kql/main/Chapter%206%3A%20Advanced%20KQL%20for%20Cyber%20Security/Data%20Samples/AADSignInLogs.csv']with(format='csv',ignorefirstrecord=true)
| mv-apply ConditionalAccessPolicies on (
where ConditionalAccessPolicies.result == "failure"
)
```

### Example of using mv-apply for mailbox rule creation
```KQL
CloudAppEvents
| where Application == "Microsoft Exchange Online"
| where ActionType == "New-InboxRule"
| mv-apply Objects=todynamic(ActivityObjects) on 
(
where Objects.Name == "Name"
| extend RuleName= Objects.Value
)
| where isnotempty(RuleName)
| where RuleName matches regex @"^[^a-zA-Z0-9]*$"
| extend AccountUpn=tostring(RawEventData.UserId)
| extend SessionId=tostring(RawEventData.SessionId)
| project TimeGenerated, Application, ActionType, AccountUpn, RuleName, SessionId, IPAddress
```
