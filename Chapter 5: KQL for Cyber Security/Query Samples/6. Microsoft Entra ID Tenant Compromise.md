## Microsoft Entra ID Compromise

#### KQL queries from the Microsoft Entra ID Compromise section of chapter 5 are found below

#### Use the copy option (to the right of each code box) to copy the query to paste into your own environment

#### The queries in this chapter are based off a fictious scenario at TailspinToys so won't produce results if you copy them directly. If you substitute real users from your tenant into the queries it should produce results for you.

##### Get the schema of the Auditlogs table
```KQL
SigninLogs
| getschema
```

##### Using take to see a sample result in Auditlogs
```KQL
SigninLogs
| take 1
```

##### Using project to return only some fields in AuditLogs
```KQL
AuditLogs
| project TimeGenerated, OperationName, Result, ResultDescription, TargetResources, InitiatedBy
```

##### Find audit events for adm-andrew.harris
```KQL
AuditLogs
| where TimeGenerated > ago (5d)
| project TimeGenerated, OperationName, Result, ResultDescription, TargetResources, InitiatedBy
| where InitiatedBy has "adm-andrew.harris@tailspintoys.onmicrosoft.com" 
or TargetResources has "adm-andrew.harris@tailspintoys.onmicrosoft.com"
```

##### Summarize audit events for adm-andrew.harris
```KQL
AuditLogs
| where TimeGenerated > ago (5d)
| project TimeGenerated, OperationName, Result, ResultDescription, TargetResources, InitiatedBy
| where InitiatedBy has "adm-andrew.harris@tailspintoys.onmicrosoft.com" 
or TargetResources has "adm-andrew.harris@tailspintoys.onmicrosoft.com"
| summarize TotalCount=count(), FirstEvent=min(TimeGenerated), LastEvent=max(TimeGenerated) by OperationName
```

##### Finding the remove member from role event
```KQL
AuditLogs
| where TimeGenerated > ago (5d)
| project TimeGenerated, OperationName, Result, ResultDescription, TargetResources, InitiatedBy
| where InitiatedBy has "adm-andrew.harris@tailspintoys.onmicrosoft.com" 
or TargetResources has "adm-andrew.harris@tailspintoys.onmicrosoft.com"
| where OperationName == "Remove member from role"
```

##### Extending nested fields to our query to make it a better detection
```KQL
AuditLogs
| where TimeGenerated > ago (5d)
| project TimeGenerated, OperationName, Result, ResultDescription, TargetResources, InitiatedBy
| where InitiatedBy has "adm-andrew.harris@tailspintoys.onmicrosoft.com"
or TargetResources has "adm-andrew.harris@tailspintoys.onmicrosoft.com"
| where OperationName == "Remove member from role" 
| extend Target = tostring(parse_json(TargetResources)[0].userPrincipalName)
| extend ActorIPAddress = tostring(parse_json(tostring(parse_json(InitiatedBy).user)).ipAddress)
| extend Actor = tostring(parse_json(tostring(parse_json(InitiatedBy).user)).userPrincipalName)
| extend RoleName = tostring(parse_json(tostring(parse_json(tostring(parse_json(TargetResources)[0].modifiedProperties))[1].oldValue)))
| project TimeGenerated, OperationName, Result, Target, RoleName, Actor, ActorIPAddress
```

##### Finding audit events for svc-integration
```KQL
AuditLogs
| where TimeGenerated > ago (5d)
| project TimeGenerated, OperationName, Result, ResultDescription, TargetResources, InitiatedBy
| where InitiatedBy has "svc-integration@tailspintoys.onmicrosoft.com" 
or TargetResources has svc-integration@tailspintoys.onmicrosoft.com
```

##### Find create user events from adn-pia.westermann
```KQL
AuditLogs
| where TimeGenerated > ago (5d)
| project TimeGenerated, OperationName, Result, ResultDescription, TargetResources, InitiatedBy
| where OperationName == "Add user" and InitiatedBy has "adm-pia.westermann@tailspintoys.onmicrosoft.com"
| extend ActorIPAddress = tostring(parse_json(tostring(InitiatedBy.user)).ipAddress)
| extend Actor = tostring(parse_json(tostring(InitiatedBy.user)).userPrincipalName)
| extend Target = tostring(TargetResources[0].userPrincipalName)
| project TimeGenerated, OperationName, Actor, ActorIPAddress, Target
```

##### MSFT Employee Contribution-Rudnei Oliveria
##### Sometimes adversaries will re-enable existing accounts and take control of them, rather than creating new accounts as a way to avoid detection. You can identify is a user has re-enabled a disabled user.
```KQL
AuditLogs 
| where OperationName == "Enable account" 
| extend userPrincipalName_ = tostring(parse_json(tostring(InitiatedBy.user)).userPrincipalName) 
| extend ipAddress_ = tostring(parse_json(tostring(InitiatedBy.user)).ipAddress) 
| extend TargetUserEnabled = tostring(TargetResources[0].userPrincipalName) 
| project TimeGenerated, OperationName, UserThatEnableUser=userPrincipalName_, IPOrigin=ipAddress_, UserUpdated=TargetUserEnabled 
```

##### Find MFA registration events for adm-pia.westermann
```KQL
AuditLogs
| where TimeGenerated > ago (5d)
| project TimeGenerated, OperationName, Result, ResultDescription, TargetResources, InitiatedBy
| where OperationName in ("Reset password (by admin)","Admin registered security info")
| where TargetResources has adm-pia.westermann@tailspintoys.onmicrosoft.com
```


##### MSFT Employee Contribution-Marius Folling
##### Using regex for searching for MFA phone number changes can be a valuable hunting strategy for understanding user compromise. Regex can be used to look for particular patterns for phone numbers, for instance if your business is in Europe, then USA formatted numbers may be suspicious 
```KQL
CloudAppEvents 
| where Timestamp >= datetime("Insert date") 
| where ActionType == "Update user." and RawEventData contains "StrongAuthentication" 
| extend target = RawEventData.ObjectId 
| mvexpand ModifiedProperties = parse_json(RawEventData.ModifiedProperties) 
| where ModifiedProperties matches regex @"\+\d{1,3}\s*\d{9,}" 
| mvexpand ModifiedProperties = parse_json(ModifiedProperties) 
| where ModifiedProperties contains "NewValue" and ModifiedProperties matches regex @"\+\d{1,3}\s*\d{9,}" 
| extend PhoneNumber = extract(@"\+\d{1,3}\s*\d{9,}", 0, tostring(ModifiedProperties)) 
| project Timestamp, target, PhoneNumber 

##### Find other MFA events for the same phone number
```KQL
AuditLogs
| where TimeGenerated > ago(5d)
| where OperationName == "Admin registered security info"
| where TargetResources has "+1 4845551234"
| extend Target = tostring(TargetResources[0].userPrincipalName)
| extend Actor = tostring(parse_json(tostring(InitiatedBy.user)).userPrincipalName)
| project TimeGenerated, OperationName, Result, Actor,Target
```

##### Find audit eventss for malicious accounts or IP address
```KQL
AuditLogs
| where TimeGenerated > ago (5d)
| project TimeGenerated, OperationName, Result, ResultDescription, TargetResources, InitiatedBy
| where InitiatedBy has_any ("svc-integration@tailspintoys.onmicrosoft.com",
"svc-useronboarding@tailspintoys.onmicrosoft.com",
"helpdesk01@tailspintoys.onmicrosoft.com",
"breakglass04@tailspintoys.onmicrosoft.com") 
or InitiatedBy has "50.50.50.50"
```

##### Find audit eventss for genuine accounts and malicious IP address
```KQL
AuditLogs
| where TimeGenerated > ago (5d)
| project TimeGenerated, OperationName, Result, ResultDescription, TargetResources, InitiatedBy
| where InitiatedBy has_any ("adm-andrew.harris@tailspintoys.onmicrosoft.com",
"adm-pia.westermann@tailspintoys.onmicrosoft.com",
"admin-jon.orton@tailspintoys.onmicrosoft.com") 
and InitiatedBy has "50.50.50.50"
```

##### Summarize audit events for malicious accounts
```KQL
AuditLogs
| where TimeGenerated > ago (5d)
| project TimeGenerated, OperationName, Result, ResultDescription, TargetResources, InitiatedBy
| where InitiatedBy has_any ("svc-integration@tailspintoys.onmicrosoft.com",
"svc-useronboarding@tailspintoys.onmicrosoft.com",
"helpdesk01@tailspintoys.onmicrosoft.com",
"breakglass04@tailspintoys.onmicrosoft.com")
| summarize TotalCount=count(), FirstEvent=min(TimeGenerated), LastEvent=max(TimeGenerated) by OperationName
```

##### Find the Add named location event
```KQL
AuditLogs
| where TimeGenerated > ago (5d)
| project TimeGenerated, OperationName, Result, ResultDescription, TargetResources, InitiatedBy
| where InitiatedBy has_any ("svc-integration@tailspintoys.onmicrosoft.com",
"svc-useronboarding@tailspintoys.onmicrosoft.com",
"helpdesk01@tailspintoys.onmicrosoft.com",
"breakglass04@tailspintoys.onmicrosoft.com")
| where OperationName == "Add named location"
```

##### Findthe update conditional access policy event
```KQL
AuditLogs
| where TimeGenerated > ago (5d)
| project TimeGenerated, OperationName, Result, ResultDescription, TargetResources, InitiatedBy
| where InitiatedBy has_any ("svc-integration@tailspintoys.onmicrosoft.com",
"svc-useronboarding@tailspintoys.onmicrosoft.com",
"helpdesk01@tailspintoys.onmicrosoft.com",
"breakglass04@tailspintoys.onmicrosoft.com")
| where OperationName == "Update conditional access policy" 
```

##### Find the update application secrets event
```KQL
AuditLogs
| where TimeGenerated > ago (5d)
| project TimeGenerated, OperationName, Result, ResultDescription, TargetResources, InitiatedBy
| where InitiatedBy has_any ("svc-integration@tailspintoys.onmicrosoft.com",
"svc-useronboarding@tailspintoys.onmicrosoft.com",
"helpdesk01@tailspintoys.onmicrosoft.com",
"breakglass04@tailspintoys.onmicrosoft.com")
| where OperationName has "Update application â€“ Certificates and secrets management"
```

##### Pivot to sign in log data to investigate malicious IP and accounts
```KQL
SigninLogs
| project TimeGenerated, UserPrincipalName, ResultType, AppDisplayName, UserAgent, Location, IPAddress, RiskLevelDuringSignIn, RiskEventTypes
| where UserPrincipalName in~ ("svc-integration@tailspintoys.onmicrosoft.com",
"svc-useronboarding@tailspintoys.onmicrosoft.com",
"helpdesk01@tailspintoys.onmicrosoft.com",
"breakglass04@tailspintoys.onmicrosoft.com") or IPAddress == "50.50.50.50"
```
