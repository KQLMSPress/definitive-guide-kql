## Phishing Attacks

#### KQL queries from the User Compromise in Microsoft 365 section of chapter 5 are found below

#### Use the copy option (to the right of each code box) to copy the query to paste into your own environment

#### The queries in this section of the book use a fictious scenario from Tailspin Toys, so you won't see any results in your environment if you run the queries directly. However if you switch in real emails and users from your tenant then you can see the results of the queries in your own environment.


##### Searching email events for a suspsicious subject
```KQL
EmailEvents
| where TimeGenerated > ago(30d)
| where Subject =~ "Your Account Will Expire in 90 days"
| project TimeGenerated, EmailDirection, RecipientEmailAddress, SenderFromAddress, SenderFromDomain, DeliveryAction, DeliveryLocation
```

##### Searching email events for a suspsicious subject, suspicious domain or specific sender
```KQL
EmailEvents
| where TimeGenerated > ago(30d)
| where Subject =~ "Your Account Will Expire in 90 days"
    or SenderFromAddress =~ "IT-team@tailspinIT.com"
    or SenderFromDomain =~ "tailspinIT.com "
| project TimeGenerated, EmailDirection, RecipientEmailAddress, SenderFromAddress, SenderFromDomain, Subject, DeliveryAction, DeliveryLocation
```

##### MSFT Employee Contribution-Tim Haintz
##### Understanding email authentication patterns can be valuable to understand suspicious email flows into your environment. Phishing attacks are less likely to have properly configured email security, and are more likely to fail authentication requirements such as SPF or DMARC.
```KQL
EmailEvents 
| where Timestamp > ago(20d) 
| extend AuthenticationDetails = todynamic(AuthenticationDetails) 
| project SenderFromAddress, 
            SenderDisplayName, 
            RecipientEmailAddress, 
            AuthDetailsSPF=parse_json(AuthenticationDetails.SPF), 
            AuthDetailsDKIM=parse_json(AuthenticationDetails.DKIM), 
            AuthDetailsDMARC=parse_json(AuthenticationDetails.DMARC), 
            AuthDetailsCompAuth=parse_json(AuthenticationDetails.CompAuth) 
| summarize by SenderFromAddress, SenderDisplayName, RecipientEmailAddress, tostring(AuthDetailsSPF), tostring(AuthDetailsDKIM), tostring(AuthDetailsDMARC), tostring(AuthDetailsCompAuth) 
```

##### Using disinct to understand DeliveryAction events
```KQL
EmailEvents
| where TimeGenerated > ago(30d)
| distinct DeliveryAction
```

##### Using count() to understand DeliveryAction events
```KQL
EmailEvents
| where TimeGenerated > ago(30d)
| summarize count() by DeliveryAction
```

##### Searching email events for only delivered email
```KQL
EmailEvents
| where TimeGenerated > ago(30d)
| where Subject =~ "Your Account Will Expire in 90 days"
    or SenderFromAddress =~ "IT-team@tailspinIT.com"
    or SenderFromDomain =~ "tailspinIT.com "
| project TimeGenerated, EmailDirection, RecipientEmailAddress, SenderFromAddress, SenderFromDomain, Subject, DeliveryAction, DeliveryLocation
| where DeliveryAction == "Delivered"
```

##### Understanding EmailUrlInfo with take
```KQL
EmailUrlInfo
| take 10
```

##### Joining email events with URL info
```KQL
EmailEvents
| where TimeGenerated > ago(30d)
| where Subject =~ "Your Account Will Expire in 90 days"
    or SenderFromAddress =~ "IT-team@tailspinIT.com"
    or SenderFromDomain =~ "tailspinIT.com "
| project TimeGenerated, EmailDirection, RecipientEmailAddress, SenderFromAddress, SenderFromDomain, Subject, DeliveryAction, DeliveryLocation, NetworkMessageId
| where DeliveryAction == "Delivered"
| join kind=inner(EmailUrlInfo) on NetworkMessageId
```

##### Understanding UrlClickEvents with take
```KQL
UrlClickEvents
| take 10
```

##### Joining email events with URL info and click events
```KQL
EmailEvents
| where TimeGenerated > ago(30d)
| where Subject =~ "Your Account Will Expire in 90 days"
    or SenderFromAddress =~ "IT-team@tailspinIT.com"
    or SenderFromDomain =~ "tailspinIT.com "
| project TimeGenerated, EmailDirection, RecipientEmailAddress, SenderFromAddress, SenderFromDomain, Subject, DeliveryAction, DeliveryLocation, NetworkMessageId
| where DeliveryAction == "Delivered"
| join kind=inner(EmailUrlInfo) on NetworkMessageId
| join kind=inner(UrlClickEvents) on Url, NetworkMessageId
```

##### Using let to pivot on phished users to find risky sign ins
```KQL
let users=
EmailEvents
| where TimeGenerated > ago(30d)
| where Subject =~ "Your Account Will Expire in 90 days"
    or SenderFromAddress =~ "IT-team@tailspinIT.com"
    or SenderFromDomain =~ "tailspinIT.com "
| project TimeGenerated, EmailDirection, RecipientEmailAddress, SenderFromAddress, SenderFromDomain, Subject, DeliveryAction, DeliveryLocation, NetworkMessageId
| where DeliveryAction == "Delivered"
| join kind=inner(EmailUrlInfo) on NetworkMessageId
| join kind=inner(UrlClickEvents) on Url, NetworkMessageId
| distinct RecipientEmailAddress;
SigninLogs
| where UserPrincipalName in~ (users)
| where RiskLevelDuringSignIn in ("high","medium")
| project TimeGenerated, UserPrincipalName, AppDisplayName, IPAddress, Location, ResultType, RiskEventTypes, RiskLevelDuringSignIn
```

##### Using let to pivot on suspicious IP addresses uncovered by phished users
```KQL
let users=
EmailEvents
| where TimeGenerated > ago(30d)
| where Subject =~ "Your Account Will Expire in 90 days"
    or SenderFromAddress =~ "IT-team@tailspinIT.com"
    or SenderFromDomain =~ "tailspinIT.com "
| project TimeGenerated, EmailDirection, RecipientEmailAddress, SenderFromAddress, SenderFromDomain, Subject, DeliveryAction, DeliveryLocation, NetworkMessageId
| where DeliveryAction == "Delivered"
| join kind=inner(EmailUrlInfo) on NetworkMessageId
| join kind=inner(UrlClickEvents) on Url, NetworkMessageId
| distinct RecipientEmailAddress;
let ips=
SigninLogs
| where UserPrincipalName in~ (users)
| where RiskLevelDuringSignIn in ("high","medium")
| project TimeGenerated, UserPrincipalName, AppDisplayName, IPAddress, Location, ResultType, RiskEventTypes, RiskLevelDuringSignIn
| distinct IPAddress;
SigninLogs
| where UserPrincipalName !in~ (users) and IPAddress in (ips)
| project TimeGenerated, UserPrincipalName, AppDisplayName, IPAddress, Location, ResultType, RiskEventTypes, RiskLevelDuringSignIn
```

##### Summarizing sign in data from malicious IP addresses
```KQL
SigninLogs
| where IPAddress in ("40.40.40.40","30.30.30.30")
| summarize TotalCount=count(), ApplicationsAccessed=make_set(AppDisplayName) by UserPrincipalName
```

##### Finding Cloud App Events from phished users and malicious IP addresses
```KQL
CloudAppEvents
| where RawEventData has_any ("30.30.30.30","40.40.40.40") and RawEventData has_any ("eric.lang@tailspintoys.com","tanja.plate@tailspintoys.com","anna.lidman@tailspintoys.com")
```






