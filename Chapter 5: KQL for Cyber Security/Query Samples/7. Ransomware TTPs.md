## Ransomware Tactics, Techniques and Procedures

#### KQL queries from the Ransomware Tactics, Techniques and Procedures section of chapter 5 are found below

#### Use the copy option (to the right of each code box) to copy the query to paste into your own environment

#### The queries in this chapter are based off a fictious scenario at TailspinToys so won't produce results if you copy them directly. If you substitute real users or similar indicators from your tenant into the queries it should produce results for you.

##### Various ways to search for indicators
```KQL
search("kql.io")
```

```KQL
search("discoverypackage.exe")
```

```KQL
search("DA39A3EE5E6B4B0D3255BFEF95601890AFD80709")
```

##### Finding which table an indicator is in
```KQL
search("discoverypackage.exe")
| distinct $table

##### Finding which tables have the RemoteUrl field
```KQL
union withsource=TableName Device*
| where isnotempty(RemoteUrl)
| distinct TableName
```

##### Using union to search across two tables
```KQL
union DeviceEvents, DeviceNetworkEvents
| where RemoteUrl has "kql.io"
```

##### Search for specific file names or file hashes
```KQL
DeviceFileEvents
| where FileName =~ "discoverypackage.exe" or SHA1 == "DA39A3EE5E6B4B0D3255BFEF95601890AFD80709"
```

##### Finding which tables have the RemoteIP field
```KQL
union withsource=TableName Device*
| where isnotempty(RemoteIP)
| distinct TableName
```

##### Using union to search across two tables
```KQL
union DeviceNetworkEvents, DeviceLogonEvents
| where RemoteIP in ("70.70.70.70","50.50.50.50")
```

##### Finding certutil.exe connecting to a public IP
```KQL
DeviceNetworkEvents
| where InitiatingProcessCommandLine has "certutil" and RemoteIPType == "Public"
| project TimeGenerated, DeviceName, InitiatingProcessAccountName, InitiatingProcessCommandLine, LocalIPType,LocalIP, RemoteIPType, RemoteIP, RemoteUrl, RemotePort
```

##### Finding risky sign ins followed by possible discovery activity
```KQL
SigninLogs
| where RiskLevelDuringSignIn in ("high","medium")
| project RiskySigninTime=TimeGenerated, SigninIP=IPAddress, UserPrincipalName
| join kind=inner(
CloudAppEvents
| where ActionType == "FileDownloaded"
| extend UserPrincipalName = tostring(RawEventData.UserId)
| extend FileName = tostring(RawEventData.SourceFileName)
| where FileName has_any ("VPN","WFH","Work from home","Citrix","password","VDI","virtual desktop","anyconnect","globalprotect")
| project DownloadTime=TimeGenerated, FileName, UserPrincipalName
) on UserPrincipalName
| extend ['Hours Between Events']=datetime_diff("hour",DownloadTime, RiskySigninTime)
| where ['Hours Between Events'] <= 6
```

##### Searching for which tables a specific device is in
```KQL
search("DESKTOP-80YMGP") 
| distinct $table
```

##### Using an asterisk to find a device
```KQL
DeviceLogonEvents
| where * contains "DESKTOP-80YMGP"
```

##### Using the not() operator to find non standard device names
```KQL
DeviceLogonEvents
| where not (RemoteDeviceName has "TST-")
```

##### Using a let statement to find first time enumeration
```KQL
let existingusers=
DeviceLogonEvents
| where TimeGenerated > ago(30d) and TimeGenerated < ago(6h)
| where ActionType== "LogonSuccess" and LogonType == "Network"
| summarize CountOfDistinctDevices=dcount(DeviceName) by AccountName, bin(TimeGenerated,1h)
| where CountOfDistinctDevices > 50
| distinct AccountName;
DeviceLogonEvents
| where TimeGenerated > ago(6h)
| where ActionType== "LogonSuccess" and LogonType == "Network"
| summarize CountOfDistinctDevices=dcount(DeviceName) by AccountName, bin(TimeGenerated,1h)
| where CountOfDistinctDevices > 50
| where AccountName !in (existingusers)
```

##### Find logon events for a specific user
```KQL
DeviceLogonEvents
| where TimeGenerated > ago(6h)
| where DeviceName =~ "ADMSERVER01.tailspintoys.com"
| where ActionType== "LogonSuccess" and LogonType == "RemoteInteractive"
| where AccountName =~ "eric.lang"
```

##### Using a let statement to find first time access to an admin server
```KQL
let existingusers=
DeviceLogonEvents
| where TimeGenerated > ago(30d) and TimeGenerated < ago(6h)
| where DeviceName =~ "ADMSERVER01.tailspintoys.com"
| where ActionType== "LogonSuccess" and LogonType == "RemoteInteractive"
| distinct AccountName;
DeviceLogonEvents
| where TimeGenerated > ago(6h)
| where DeviceName =~ "ADMSERVER01.tailspintoys.com"
| where ActionType== "LogonSuccess" and LogonType == "RemoteInteractive"
| where AccountName !in (existingusers)
```

##### Using Security Event data to find first time access to a device
```KQL
let existingusers=
SecurityEvent
| where TimeGenerated > ago(30d) and TimeGenerated < ago(6h)
| where Computer == "ADMSERVER01.tailspintoys.com"
| where EventID == 4624
| where LogonType == 10
| distinct TargetAccount;
SecurityEvent
| where TimeGenerated > ago(6h)
| where Computer == "ADMSERVER01.tailspintoys.com"
| where EventID == 4624
| where LogonType == 10
| where TargetAccount !in (existingusers)
```

##### Find attempts at Windows Defender tampering
```KQL
DeviceEvents
| where ActionType == "TamperingAttempt"
```

##### Find .dmp files created
```KQL
DeviceFileEvents
| where InitiatingProcessFileName != "WerFault.exe"
| where FileName endswith ".dmp"
```

##### Find users accessing Domain Controllers from non PAW machines
```KQL
DeviceLogonEvents
| where DeviceName startswith "ADDC"
| where ActionType == "LogonSuccess" and LogonType == "RemoteInteractive"
| where RemoteDeviceName !startswith "PAW-"
```

##### Find bulk changes to Windows Defender configuration
```KQL
Event
| where EventLog == "Microsoft-Windows-Windows Defender/Operational"
| where EventID in ("5001","5007","5013","1116")
| summarize CountofDistinctDevices=dcount(Computer), ListofDevices=make_set(Computer) by EventID, bin(TimeGenerated, 15m)
| where CountofDistinctDevices > 10
```


