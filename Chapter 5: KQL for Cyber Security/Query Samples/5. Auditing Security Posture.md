## Auditing Security Posture

#### KQL queries from the Auditing Security Posture section of chapter 5 are found below

#### Use the copy option (to the right of each code box) to copy the query to paste into your own environment

#### The queries from this data were run against a lab environment, if you run it against your environment you should see results for your environment, assuming you have the data available

##### Get the schema of the SigninLogs table
```KQL
SigninLogs
| getschema
```

##### Using take to see a sample result in SigninLogs
```KQL
SigninLogs
| take 1
```

##### Using project to only return certain fields
```KQL
SigninLogs
| project TimeGenerated, AppDisplayName, UserPrincipalName, ResultType, AuthenticationRequirement, Location
```

##### Counting multifactor vs singlefactor authentication
```KQL
SigninLogs
| where TimeGenerated > ago (180d)
| where ResultType == 0
| project TimeGenerated, AppDisplayName, UserPrincipalName, ResultType, ResultDescription,AuthenticationRequirement, Location
| summarize Count=count() by AuthenticationRequirement
```

##### Counting total sign ins and multifactor vs singlefactor using countif()
```KQL
SigninLogs
| where TimeGenerated > ago (180d)
| where ResultType == 0
| project TimeGenerated, AppDisplayName, UserPrincipalName, ResultType, ResultDescription,AuthenticationRequirement, Location
| summarize TotalCount=count(),MultiFactor=countif(AuthenticationRequirement == "multiFactorAuthentication"), SingleFactor=countif(AuthenticationRequirement == "singleFactorAuthentication")
```

##### Adding percentages to our counts
```KQL
SigninLogs
| where TimeGenerated > ago (180d)
| where ResultType == 0
| project TimeGenerated, AppDisplayName, UserPrincipalName, ResultType, ResultDescription,AuthenticationRequirement, Location
| summarize TotalCount=count(),MultiFactor=countif(AuthenticationRequirement == "multiFactorAuthentication"), SingleFactor=countif(AuthenticationRequirement == "singleFactorAuthentication")
| extend ['MFA Percentage']=(todouble(MultiFactor) * 100 / todouble(TotalCount))
| extend ['SFA Percentage']=(todouble(SingleFactor) * 100 / todouble(TotalCount))
| project-reorder TotalCount, MultiFactor, ['MFA Percentage'], SingleFactor, ['SFA Percentage']
```

##### Using round to limit decimal points
```KQL
SigninLogs
| where TimeGenerated > ago (180d)
| where ResultType == 0
| project TimeGenerated, AppDisplayName, UserPrincipalName, ResultType, ResultDescription,AuthenticationRequirement, Location
| summarize TotalCount=count(),MultiFactor=countif(AuthenticationRequirement == "multiFactorAuthentication"), SingleFactor=countif(AuthenticationRequirement == "singleFactorAuthentication")
| extend ['MFA Percentage']=round((todouble(MultiFactor) * 100 / todouble(TotalCount)),2)
| extend ['SFA Percentage']=round((todouble(SingleFactor) * 100 / todouble(TotalCount)),2)
| project-reorder TotalCount, MultiFactor, ['MFA Percentage'], SingleFactor, ['SFA Percentage']
```

##### Calculating MFA percentage per application
```KQL
SigninLogs
| where TimeGenerated > ago (180d)
| where ResultType == 0
| project TimeGenerated, AppDisplayName, UserPrincipalName, ResultType, ResultDescription,AuthenticationRequirement, Location
| summarize TotalCount=count(),MultiFactor=countif(AuthenticationRequirement == "multiFactorAuthentication"), SingleFactor=countif(AuthenticationRequirement == "singleFactorAuthentication") by AppDisplayName
| extend ['MFA Percentage']=round((todouble(MultiFactor) * 100 / todouble(TotalCount)),2)
| extend ['SFA Percentage']=round((todouble(SingleFactor) * 100 / todouble(TotalCount)),2)
| project-reorder AppDisplayName, TotalCount, MultiFactor, ['MFA Percentage'], SingleFactor, ['SFA Percentage']
```


##### Calculating MFA percentage per user
```KQL
SigninLogs
| where TimeGenerated > ago (180d)
| where ResultType == 0
| project TimeGenerated, AppDisplayName, UserPrincipalName, ResultType, ResultDescription,AuthenticationRequirement, Location
| summarize TotalCount=count(),MultiFactor=countif(AuthenticationRequirement == "multiFactorAuthentication"), SingleFactor=countif(AuthenticationRequirement == "singleFactorAuthentication") by UserPrincipalName
| extend ['MFA Percentage']=round((todouble(MultiFactor) * 100 / todouble(TotalCount)),2)
| extend ['SFA Percentage']=round((todouble(SingleFactor) * 100 / todouble(TotalCount)),2)
| project-reorder UserPrincipalName, TotalCount, MultiFactor, ['MFA Percentage'], SingleFactor, ['SFA Percentage']
```

##### MSFT Employee Contribution-Cosmin Guliman
##### Legacy authentication is important to also track, as it is not MFA aware. This query will return a breakdown of modern vs legacy TLS sessions, summarized by applicationId and application display name. The same logic can be easily translated to interactive sign-in sessions by replacing the function used to SigninLogs. You can easily breakdown sessions over time by specifying the lookback period in the query, then summarize your results by minutes / hours / days.
```KQL
AADNonInteractiveUserSignInLogs 
| extend DeviceRaw=parse_json(DeviceDetail) 
| extend DeviceOS=DeviceRaw.operatingSystem, DeviceId=DeviceRaw.deviceId,DeviceBrowser=DeviceRaw.browser 
| where AuthenticationProcessingDetails has "Legacy TLS" 
| extend JsonAuthProcDetails = parse_json(AuthenticationProcessingDetails) 
| mv-apply JsonAuthProcDetails on (  
where JsonAuthProcDetails.key startswith "Legacy TLS"  
| project HasLegacyTls=JsonAuthProcDetails.value)  
| summarize Total=count(),LegacyTLS=countif(HasLegacyTls == true), ModernTLS=countif(HasLegacyTls != true) by AppDisplayName, AppId, tostring(DeviceOS), tostring(DeviceRaw), UserDisplayName, UserId, UserPrincipalName 
```

##### Counting total users
```KQL
SigninLogs
| where TimeGenerated > ago(30d)
| where ResultType == 0
| distinct UserPrincipalName
| count
```

##### Counting guest accounts
```KQL
SigninLogs
| where TimeGenerated > ago(30d)
| where ResultType == 0
| where UserType == "Guest"
| distinct UserPrincipalName
| count
```

##### Counting account type per application
```KQL
SigninLogs
| where TimeGenerated > ago(30d)
| where ResultType == 0
| summarize Members=dcountif(UserPrincipalName,UserType == "Member"),Guests=dcountif(UserPrincipalName,UserType == "Guest") by AppDisplayName
```

##### Calculating last sign in time per user
```KQL
SigninLogs
| where TimeGenerated > ago(180d)
| where ResultType == 0
| where UserPrincipalName startswith "adm"
| summarize LastSignIn=max(TimeGenerated) by UserPrincipalName
```

##### Counting days since last sign in per user using datetime_diff()
```KQL
SigninLogs
| where TimeGenerated > ago(180d)
| where ResultType == 0
| summarize LastSignIn=max(TimeGenerated) by UserPrincipalName
| extend DaysSinceLastLogon=datetime_diff('day',now(),LastSignIn)
```

##### Counting days since last sign in per application using datetime_diff()
```KQL
SigninLogs
| where TimeGenerated > ago(180d)
| where ResultType == 0
| summarize LastSignIn=max(TimeGenerated) by AppDisplayName
| extend DaysSinceLastLogon=datetime_diff('day',now(),LastSignIn)
```

##### Counting operating system statistics
```KQL
DeviceInfo
| where TimeGenerated > ago(30d)
| summarize arg_max(TimeGenerated, *) by DeviceId
| summarize Count=count() by OSPlatform
```

##### Visualizing operating system statistics
```KQL
DeviceInfo
| where TimeGenerated > ago(30d)
| summarize arg_max(TimeGenerated, *) by DeviceId
| summarize Count=count() by OSPlatform
| sort by Count
| render barchart
```

##### Calculating Windows Defender sensor state
```KQL
DeviceInfo
| where TimeGenerated > ago(30d)
| summarize arg_max(TimeGenerated, *) by DeviceId
| where OnboardingStatus == "Onboarded"
| summarize Count=count() by SensorHealthState
```

##### Finding devices with a public IP
```KQL
DeviceInfo
| where TimeGenerated > ago(30d)
| summarize arg_max(TimeGenerated, *) by DeviceId
| where isnotempty(PublicIP)
| project DeviceName, PublicIP, OSPlatform
```

##### Finding devices with inbound connections from public IP addresses
```KQL
DeviceNetworkEvents
| where TimeGenerated > ago (30d)
| where ActionType == "InboundConnectionAccepted" and RemoteIPType == "Public"
| distinct DeviceName
```

##### Finding devices with inbound connections from public IP addresses on specific ports
```KQL
DeviceNetworkEvents
| where TimeGenerated > ago (30d)
| where ActionType == "InboundConnectionAccepted" and RemoteIPType == "Public"
| where LocalPort in (22,80,443,3389)
| project TimeGenerated, DeviceName, RemoteIP, RemotePort, LocalIP, LocalPort
```

##### Finding successful logon events from public IP addresses
```KQL
DeviceLogonEvents
| where TimeGenerated > ago (30d)
| where ActionType == "LogonSuccess" and RemoteIPType == "Public"
| project TimeGenerated, DeviceName, AccountName, LogonType, RemoteIP
```

##### Finding local admin logons to devices
```KQL
DeviceLogonEvents
| where TimeGenerated > ago (340d)
| where ActionType == "LogonSuccess"
| where LogonType == "Interactive"
| where InitiatingProcessCommandLine == "lsass.exe"
| where AdditionalFields.IsLocalLogon == true
| where IsLocalAdmin == true
| project TimeGenerated, DeviceName, AccountName, LogonType, IsLocalAdmin
```

##### Summarizing local admin logon events to find risky devices
```KQL
DeviceLogonEvents
| where TimeGenerated > ago (30d)
| where ActionType == "LogonSuccess"
| where LogonType == "Interactive"
| where InitiatingProcessCommandLine == "lsass.exe"
| where AdditionalFields.IsLocalLogon == true
| where IsLocalAdmin == true
| project TimeGenerated, DeviceName, AccountName, LogonType, IsLocalAdmin
| summarize CountofAdmins=dcount(AccountName), ListofAdmins=make_set(AccountName) by DeviceName
```

##### Summarizing local admin logon events to find risky users
```KQL
DeviceLogonEvents
| where TimeGenerated > ago (30d)
| where ActionType == "LogonSuccess"
| where LogonType == "Interactive"
| where InitiatingProcessCommandLine == "lsass.exe"
| where AdditionalFields.IsLocalLogon == true
| where IsLocalAdmin == true
| project TimeGenerated, DeviceName, AccountName, LogonType, IsLocalAdmin
| summarize CountofDevices=dcount(DeviceName), ListofDevices=make_set(DeviceName) by AccountName
```

##### Find devices with specific software
```KQL
DeviceTvmSoftwareInventory
| where SoftwareName has "onedrive"
```

##### Find devices with a specific vulnerability
```KQL
DeviceTvmSoftwareVulnerabilities
| where CveId == @"CVE-2022-38013"
```

##### Summarize devices with high severity CVEs
```KQL
DeviceTvmSoftwareVulnerabilities
| where VulnerabilitySeverityLevel == "High"
| summarize CountOfHighVulns=dcount(CveId) by DeviceName
```

##### Summarize devices with public IP addresses and high CVE's
```KQL
let devices=
DeviceNetworkEvents
| where ActionType == "InboundConnectionAccepted" and RemoteIPType == "Public"
| distinct DeviceName;
DeviceTvmSoftwareVulnerabilities
| where DeviceName in (devices)
| where VulnerabilitySeverityLevel == "High"
| summarize CountOfHighVulns=dcount(CveId) by DeviceName
```
